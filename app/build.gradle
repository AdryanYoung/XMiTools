apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

def key = file(findProperty("tianma.keystore.path") as String)
def propFile = file(findProperty("tianma.signature.path") as String)

def keyProps = new Properties()
if (propFile.exists()) {
    keyProps.load(new FileInputStream(propFile))
}

def static releaseTime() {
    return new Date().format("yyMMdd", TimeZone.default)
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.tianma.tweaks.miui"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 11
        versionName "1.1.0"

        buildConfigField("String", "LOG_TAG", "\"XMiTools\"")
        buildConfigField("boolean", "LOG_TO_XPOSED", "true")
        buildConfigField("boolean", "LOG_TO_EDXPOSED", "true")
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    signingConfigs {
        release {
            storeFile key
            storePassword keyProps['STORE_PASSWORD']
            keyAlias keyProps['KEY_ALIAS']
            keyPassword keyProps['KEY_PASSWORD']
        }
    }

    buildTypes {
        debug {
            buildConfigField("int", "LOG_LEVEL", "2")

            if (key.exists()) {
                println "using keystore"
                signingConfig signingConfigs.release
            }
        }

        release {
            buildConfigField("int", "LOG_LEVEL", "4")

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (key.exists()) {
                println "using keystore"
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "XMiTools_${variant.versionName.replaceAll('\\s+', '_')}_${releaseTime()}_${variant.buildType.name.charAt(0)}.apk"
        }
    }
}

ext {
    gsonVersion = "2.8.6" // gson

    rxJavaVersion = "2.2.10" // RxJava 2
    rxAndroidVersion = "2.1.1" // RxAndroid 2

    okHttpVersion = "3.14.4" // OkHttp
    retrofitVersion = "2.5.0" // retrofit
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0' // material design support
    implementation 'androidx.preference:preference:1.1.0' // preference-v7
    implementation 'androidx.browser:browser:1.2.0' // custom tabs

    // Xposed
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'

    // ButterKnife
    implementation 'com.jakewharton:butterknife:10.2.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'

    // Android Shell
    implementation 'com.jaredrummler:android-shell:1.0.0'

    // Material Dialogs
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'

    // color picker preference
    implementation 'com.jaredrummler:colorpicker:1.1.0'

    // Gson
    implementation "com.google.code.gson:gson:$gsonVersion"

    // RxJava 2
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    // RxAndroid 2
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    // OkHttp logging interceptor
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    // Retrofit Gson converter
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    // Retrofit scalars converter
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    // Retrofit RxJava Adapter
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // Kotlin
    implementation "androidx.core:core-ktx:1.2.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
